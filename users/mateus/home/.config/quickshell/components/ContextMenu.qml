pragma ComponentBehavior: Bound
import QtQuick
import Quickshell
import "../core"

PopupWindow {
id: menuPopup

readonly property int menuWidth: 200
readonly property int itemHeight: 26
readonly property int separatorHeight: 8
readonly property int verticalOffset: 5
readonly property int menuMargins: 6
readonly property int menuMaxHeight: 450
property int _pendingX: 0
property int _pendingY: 0

readonly property string filterText: searchInput.text

property var menuModel: null
property var menuStack: []
property var _headerMenuModel: []
property var _mainMenuModel: []
property var _footerMenuModel: []
property var _pendingWindow: null
property var _pendingAnchorItem: null
property var _currentAnchorItem: null
property var _lastAnchorItem: null

property bool _isAnchorMode: false
property bool _isInternalReset: false
property bool _isPreparing: false
property bool showSearchInput: false
readonly property bool isMenuFocused: visible || _isPreparing
readonly property bool isClosing: closeAnim.running

color: "transparent"

function _dyn(obj) {
return obj;
}

function shouldOpenFor(anchorItem) {
if (visible && _currentAnchorItem === anchorItem) {
close();
return false;
}

if (isClosing && _lastAnchorItem === anchorItem) return false;

return true;
}

function _restoreFocus() {
if (!visible) return;

if (showSearchInput)
searchInput.forceFocusNow();
else
menuBackground.forceActiveFocus();
}

function pushMenu(modelData, tag, refreshFn) {
menuStack.push({
model: modelData,
tag: tag || "",
refreshFn: refreshFn || null
});

_updateMenuFromStack();
_restoreFocus();
}

function popMenu() {
if (menuStack.length > 1) {
menuStack.pop();
_updateMenuFromStack();
_restoreFocus();
} else {
close();
}
}

function refresh() {
if (!visible)
return;

for (let i = 0; i < menuStack.length; ++i) {
const entry = menuStack[i];

if (typeof entry.refreshFn === "function") {
const updated = entry.refreshFn();
entry.model = updated ?? [];
}
}

_updateMenuFromStack();
}

function _splitMenuModel(modelData) {
let headerItems = [];
let mainItems = [];
let footerItems = [];

if (Array.isArray(modelData)) {
for (const item of modelData) {
if (!item) continue;

if (item.__fixedHeader === true) {
headerItems.push(item);
} else if (item.__fixedFooter === true) {
footerItems.push(item);
} else {
mainItems.push(item);
}
}
} else {
mainItems = modelData || [];
}

_headerMenuModel = headerItems;
_mainMenuModel = mainItems;
_footerMenuModel = footerItems;
}

function _updateMenuFromStack() {
if (menuStack.length === 0) {
menuPopup.menuModel = null;
_headerMenuModel = [];
_mainMenuModel = [];
_footerMenuModel = [];
return;
}

const topEntry = menuStack[menuStack.length - 1];
const currentModel = topEntry.model;

menuPopup.menuModel = currentModel;

_splitMenuModel(currentModel);
_updateFilteredModel();
}

readonly property alias menuView: menuView

readonly property bool _isDirectModel: menuPopup.menuModel !== null && (Array.isArray(menuPopup.menuModel) || typeof menuPopup.menuModel.rowCount === "function" || menuPopup.menuModel.count !== undefined)

readonly property var _unfilteredModel: menuPopup._isDirectModel ? menuPopup._mainMenuModel : menuOpener.children

property var _currentFilteredModel: []

onFilterTextChanged: _updateFilteredModel()

signal itemTriggered(var itemData)
signal itemDataActionTriggered(string actionType, var data)

implicitWidth: menuWidth

implicitHeight: Math.min((menuPopup.showSearchInput ? searchInput.height + 4 : 0) + headerContainer.implicitHeight + menuView.contentHeight + footerContainer.implicitHeight + (menuMargins * 2), menuMaxHeight)

grabFocus: true

onVisibleChanged: {
if (!visible && !_isInternalReset) {
searchInput.text = "";
menuView.currentIndex = -1;
menuPopup.showSearchInput = false;
menuPopup.menuModel = null;
menuPopup.menuStack = [];
menuPopup._headerMenuModel = [];
menuPopup._mainMenuModel = [];
menuPopup._footerMenuModel = [];
menuPopup._currentFilteredModel = [];
menuPopup._currentAnchorItem = null;
menuPopup._pendingWindow = null;
menuPopup._pendingAnchorItem = null;
menuPopup._pendingX = 0;
menuPopup._pendingY = 0;
menuPopup._dyn(menuPopup).anchor.window = null;
menuBackground.opacity = 0.0;
menuBackground.scale = 0.95;
}
}

function close() {
if (!visible || isClosing) return;
_lastAnchorItem = _currentAnchorItem;
closeAnim.start();
}

function _finalizeClose() {
visible = false;
}

function openMenu(targetWindow, anchorItem, modelData, tag, refreshFn) {
if (!anchorItem) return;

_prepareToOpen(targetWindow, modelData, tag, refreshFn);

_pendingAnchorItem = anchorItem;
_isAnchorMode = true;

Qt.callLater(_applyPositioning);
}

function openAtPosition(targetWindow, x, y, modelData, tag, refreshFn) {
if (!targetWindow) return;

_prepareToOpen(targetWindow, modelData, tag, refreshFn);

_pendingX = x;
_pendingY = y;
_isAnchorMode = false;

Qt.callLater(_applyPositioning);
}

function _prepareToOpen(targetWindow, modelData, tag, refreshFn) {
_isPreparing = true;

closeAnim.stop();

_pendingAnchorItem = null;
_currentAnchorItem = null;

menuPopup.menuModel = null;
menuPopup._mainMenuModel = [];
menuPopup._footerMenuModel = [];

searchInput.text = "";

_isInternalReset = true;
visible = false;
_isInternalReset = false;

menuPopup.menuStack = [{
model: modelData,
tag: tag || "main",
refreshFn: refreshFn || null
}];

_pendingWindow = targetWindow;
}

function handleItemTrigger(dataObj) {
if (!dataObj || dataObj.enabled === false || dataObj.isSeparator || dataObj.type === "separator") return;

itemTriggered(dataObj);

if (dataObj.actionType !== undefined) itemDataActionTriggered(dataObj.actionType, dataObj.actionData);

if (dataObj.onTrigger) dataObj.onTrigger();
else if (dataObj.triggered) dataObj.triggered();

if (dataObj.closeOnTrigger !== false && !dataObj.preventClose) close();
}

function _applyPositioning() {
if (!_pendingWindow) {
_isPreparing = false;
return;
}

_updateMenuFromStack();

menuPopup._dyn(menuPopup).anchor.window = _pendingWindow;

if (_isAnchorMode) {
if (!_pendingAnchorItem) {
_isPreparing = false;
return;
}

const windowPos = _pendingAnchorItem.mapToItem(null, 0, _pendingAnchorItem.height);

const newX = windowPos.x - (implicitWidth / 2) + (_pendingAnchorItem.width / 2);
const newY = windowPos.y + verticalOffset;

menuPopup._dyn(menuPopup).anchor.rect = Qt.rect(newX, newY, _pendingAnchorItem.width, 1);

menuBackground.transformOrigin = Item.Top;
} else {
menuPopup._dyn(menuPopup).anchor.rect = Qt.rect(_pendingX, _pendingY, 1, 1);

menuBackground.transformOrigin = Item.Center;
}

_currentAnchorItem = _pendingAnchorItem;

menuBackground.opacity = 0.0;
menuBackground.scale = 0.95;

menuPopup.visible = true;

openAnim.restart();

_isPreparing = false;

Qt.callLater(_restoreFocus);
}

QsMenuOpener {
id: menuOpener
menu: menuPopup._isDirectModel ? null : menuPopup.menuModel
}

function _itemText(item) {
if (!item) return "";
if (typeof item === "string") return item;
return item.text ?? item.name ?? item.label ?? item.modelData?.text ?? "";
}

function _updateFilteredModel() {
const search = menuPopup.filterText.toLowerCase().trim();

if (search === "") {
_currentFilteredModel = [];
return;
}

const rawSource = menuPopup._isDirectModel ? menuPopup._mainMenuModel : menuOpener.children;

if (!rawSource) {
_currentFilteredModel = [];
return;
}

const filtered = [];

for (const item of rawSource) {
if (_itemText(item).toLowerCase().includes(search)) filtered.push(item);
}

_currentFilteredModel = filtered;
}

function focusListView() {
if (menuView.currentIndex === -1 && menuView.count > 0) menuView.currentIndex = 0;

menuView.forceActiveFocus();
}

ParallelAnimation {
id: openAnim

NumberAnimation {
target: menuBackground
property: "opacity"
to: 1.0
duration: 150
easing.type: Easing.OutBack
easing.overshoot: 1.2
}

NumberAnimation {
target: menuBackground
property: "scale"
to: 1.0
duration: 150
easing.type: Easing.OutBack
easing.overshoot: 1.2
}
}

ParallelAnimation {
id: closeAnim

NumberAnimation {
target: menuBackground
property: "opacity"
to: 0.0
duration: 120
easing.type: Easing.OutCubic
}

NumberAnimation {
target: menuBackground
property: "scale"
to: 0.95
duration: 120
easing.type: Easing.OutCubic
}

onFinished: menuPopup._finalizeClose()
}

Rectangle {
id: menuBackground

anchors.fill: parent

color: ThemeEngine.palette.backgroundColor
border.color: ThemeEngine.dynamicBorderColor
border.width: 1
focus: true
radius: ThemeEngine.palette.shellRadius
clip: true

opacity: 0.0
scale: 0.95

MouseArea {
anchors.fill: parent

acceptedButtons: Qt.LeftButton | Qt.RightButton

onPressed: mouse => {
menuBackground.forceActiveFocus();
mouse.accepted = false;
}
}

Keys.onPressed: event => {
switch (event.key) {
case Qt.Key_Escape:
menuPopup.close();
event.accepted = true;
break;

case Qt.Key_Tab:
if (menuPopup.showSearchInput) searchInput.forceFocusNow();
event.accepted = true;
break;

case Qt.Key_Up:
if (menuView.currentIndex <= 0) {
menuView.currentIndex = menuView.count - 1;
} else {
menuView.decrementCurrentIndex();
}
event.accepted = true;
break;

case Qt.Key_Down:
if (menuView.currentIndex === -1 || menuView.currentIndex === menuView.count - 1) {
menuView.currentIndex = 0;
} else {
menuView.incrementCurrentIndex();
}
event.accepted = true;
break;

case Qt.Key_Return:
case Qt.Key_Enter:
if (menuView.currentIndex >= 0 && menuView.currentItem) {
const dataObj = menuPopup._dyn(menuView.currentItem).itemData;

if (dataObj) menuPopup.handleItemTrigger(dataObj);
}
event.accepted = true;
break;
}
}

MenuSearchInput {
id: searchInput

anchors.top: parent.top
anchors.left: parent.left
anchors.right: parent.right
anchors.margins: menuPopup.menuMargins
anchors.bottomMargin: 0

visible: menuPopup.showSearchInput
enabled: visible

itemHeight: menuPopup.itemHeight

onNavigationDownRequested: menuPopup.focusListView()

onActionTriggeredRequested: {
if (menuView.count > 0) {
const targetItem = menuView.currentItem ? menuView.currentItem : menuView.itemAtIndex(0);

if (targetItem) {
const dynTarget = menuPopup._dyn(targetItem);

if (dynTarget.itemData) menuPopup.handleItemTrigger(dynTarget.itemData);
}
}
}
}

Item {
id: headerContainer

visible: menuPopup._headerMenuModel.length > 0

anchors.left: parent.left
anchors.right: parent.right

anchors.top: searchInput.visible ? searchInput.bottom : parent.top

anchors.leftMargin: menuPopup.menuMargins
anchors.rightMargin: menuPopup.menuMargins

anchors.topMargin: searchInput.visible ? 4 : menuPopup.menuMargins

implicitHeight: headerColumn.implicitHeight

Column {
id: headerColumn

width: parent.width
spacing: 2

Repeater {
model: menuPopup._headerMenuModel

delegate: MenuItemDelegate {
required property var modelData

width: headerColumn.width

itemHeight: menuPopup.itemHeight
separatorHeight: menuPopup.separatorHeight

itemData: modelData

onTriggered: dataObj => menuPopup.handleItemTrigger(dataObj)
}
}

Rectangle {
width: parent.width
height: 1

color: ThemeEngine.dynamicBorderColor
opacity: 0.7
}
}
}

ListView {
id: menuView

anchors.top: headerContainer.visible ? headerContainer.bottom : (searchInput.visible ? searchInput.bottom : parent.top)
anchors.left: parent.left
anchors.right: parent.right
anchors.bottom: footerContainer.visible ? footerContainer.top : parent.bottom

anchors.margins: menuPopup.menuMargins
anchors.topMargin: headerContainer.visible ? 4 : (searchInput.visible ? 4 : menuPopup.menuMargins)
anchors.bottomMargin: footerContainer.visible ? 4 : menuPopup.menuMargins

highlightMoveDuration: 0
spacing: 2

interactive: true
boundsBehavior: Flickable.StopAtBounds
clip: true

currentIndex: -1
highlightFollowsCurrentItem: true

onModelChanged: currentIndex = -1

model: menuPopup.filterText.trim() === "" ? menuPopup._mainMenuModel : menuPopup._currentFilteredModel

delegate: MenuItemDelegate {
required property var model

width: menuView.width

itemHeight: menuPopup.itemHeight

separatorHeight: menuPopup.separatorHeight

itemData: model.modelData !== undefined ? model.modelData : model

onTriggered: dataObj => menuPopup.handleItemTrigger(dataObj)
}
}

Item {
id: footerContainer

visible: menuPopup._footerMenuModel.length > 0

anchors.left: parent.left
anchors.right: parent.right
anchors.bottom: parent.bottom

anchors.leftMargin: menuPopup.menuMargins
anchors.rightMargin: menuPopup.menuMargins
anchors.bottomMargin: menuPopup.menuMargins

implicitHeight: footerColumn.implicitHeight

Column {
id: footerColumn

width: parent.width
spacing: 2

Rectangle {
width: parent.width
height: 1
color: ThemeEngine.dynamicBorderColor
opacity: 0.7
}

Item {
width: 1
height: 2
}

Repeater {
model: menuPopup._footerMenuModel

delegate: MenuItemDelegate {
required property var modelData

width: footerColumn.width

itemHeight: menuPopup.itemHeight

separatorHeight: menuPopup.separatorHeight

itemData: modelData

onTriggered: dataObj => menuPopup.handleItemTrigger(dataObj)
}
}
}
}
}
}
